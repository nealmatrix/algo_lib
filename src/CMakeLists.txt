file(GLOB_RECURSE SRC "*.cpp")

add_library(${ALGO_LIB} ${SRC})

target_include_directories(${ALGO_LIB} PUBLIC 
    $<BUILD_INTERFACE:${ALGO_LIB_PUBLIC_HEADER}>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

# find_package(Eigen3 3.4 REQUIRED NO_MODULE)
# target_link_libraries(${ALGO_LIB} Eigen3::Eigen)

set_property(TARGET ${ALGO_LIB} PROPERTY VERSION ${PROJECT_VERSION})
set_property(TARGET ${ALGO_LIB} PROPERTY SOVERSION ${PROJECT_VERSION_MAJOR})
set_property(TARGET ${ALGO_LIB} PROPERTY INTERFACE_${ALGO_LIB}_MAJOR_VERSION ${PROJECT_VERSION_MAJOR})
set_property(TARGET ${ALGO_LIB} APPEND PROPERTY COMPATIBLE_INTERFACE_STRING ${ALGO_LIB}_MAJOR_VERSION)

install(TARGETS ${ALGO_LIB} 
    EXPORT ${ALGO_LIB}Targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

install(DIRECTORY ${ALGO_LIB_PUBLIC_HEADER}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

install(EXPORT ${ALGO_LIB}Targets
    FILE ${ALGO_LIB}Targets.cmake
    NAMESPACE ${ALGO_LIB}::
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${ALGO_LIB}
)